WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }

boolean = @{ "true" | "false" }
number = @{ ASCII_DIGIT+ }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
deref_ident = @{ "*" ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

add = { "+" }
less = { "<" }
not = { "~" }
binary_operator = _{ add | less }
unary_operator = _{ not }
term = _{ number | boolean | deref_ident | ident }
unary_expr = { unary_operator ~ expr }
binary_expr = { term ~ (binary_operator ~ term)* }
expr = { unary_expr | binary_expr | term }

store_assign = { "let" ~ ident ~ "=" ~ expr }
heap_new = { "let" ~ ident ~ "<-" ~ expr }
heap_update = { ident ~ "<-" ~ expr }
heap_alias = { ident ~ "=" ~ ident }
conditional = { "if" ~ expr ~ "then" ~ "\n" ~ stmnt ~ "\n" ~ "else" ~ "\n" ~ stmnt ~ "\n" ~ "fi" }
while_loop = { "while" ~ expr ~ "do" ~ "\n" ~ stmnt ~ "\n" ~ "end" }
skip = { "skip" }
stmnt = _{ store_assign
         | heap_new
         | heap_update
         | heap_alias
         | conditional
         | while_loop
         | skip }

program = _{ SOI ~ "\n"* ~ stmnt ~ ("\n"* ~ stmnt)* ~ "\n"* ~ EOI }
