WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!newline ~ ANY)* }

boolean = @{ "true" | "false" }
number = @{ ASCII_DIGIT+ }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
deref_ident = @{ "*" ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

add = { "+" }
less = { "<" }
not = { "~" }
and = { "&" }
binary_operator = _{ add | less | and }
unary_operator = _{ not }
term = _{ number | boolean | deref_ident | ident }
compound_expr = { unary_operator* ~ term ~ (binary_operator ~ unary_operator* ~ term)* }
expr = { compound_expr | term }
newline = _{ "\r" ~ "\n" | "\n" }

store_assign = { "let" ~ ident ~ "=" ~ expr }
heap_new = { "let" ~ ident ~ "<-" ~ expr }
heap_update = { ident ~ "<-" ~ expr }
heap_alias = { ident ~ "=" ~ ident }
conditional = { "if" ~ expr ~ "then" ~ newline ~ stmnt ~ newline ~ "else" ~ newline ~ stmnt ~ newline ~ "fi" }
while_loop = { "while" ~ expr ~ "do" ~ newline ~ stmnt ~ newline ~ "end" }
skip = { "skip" }
stmnt = _{ store_assign
         | heap_new
         | heap_update
         | heap_alias
         | conditional
         | while_loop
         | skip }

program = _{ SOI ~ newline* ~ stmnt ~ (newline+ ~ stmnt)* ~ newline* ~ EOI }
